// Has to be regenerated by calling the regen proc manually, elsewhere.
/obj/item/clothing/suit/roguetown/armor/manual
    name = "manually regenerating skin"
    break_sound = 'sound/foley/cloth_rip.ogg'
    drop_sound = 'sound/foley/dropsound/cloth_drop.ogg'
    icon_state = null
    slot_flags = ITEM_SLOT_SHIRT|ITEM_SLOT_ARMOR

    resistance_flags = FIRE_PROOF
    body_parts_covered = COVERAGE_FULL
    body_parts_inherent = COVERAGE_FULL
    flags_inv = null //Exposes the chest and-or breasts.
    surgery_cover = FALSE //Should permit surgery and other invasive processes.
    r_sleeve_status = SLEEVE_NORMAL
    l_sleeve_status = SLEEVE_NORMAL
    armor_class = ARMOR_CLASS_LIGHT
    blocksound = SOFTUNDERHIT
    blade_dulling = DULLING_BASHCHOP
    armor = ARMOR_PADDED

    var/repairmsg_end = "My skin has become taut with newfound vigor!"
    var/repairmsg_continue = "My armour mends some of its abuse.."

/obj/item/clothing/suit/roguetown/armor/manual/Initialize(mapload)
	..()
	ADD_TRAIT(src, TRAIT_NODROP, CURSED_ITEM_TRAIT)

/obj/item/clothing/suit/roguetown/armor/manual/dropped(mob/living/carbon/human/user)
	..()
	if(QDELETED(src))
		return
	qdel(src)

/obj/item/clothing/suit/roguetown/armor/manual/proc/armour_regen(var/repair_percent = 0.2 * max_integrity)
    if(obj_integrity >= max_integrity)
        to_chat(loc, span_notice(repairmsg_end))
    to_chat(loc, span_notice(repairmsg_continue))
    obj_integrity = min(obj_integrity + repair_percent, max_integrity)
    if(obj_broken)
        obj_fix(full_repair = FALSE)


/*
 * PUSHUP ARMOUR
 */

/obj/item/clothing/suit/roguetown/armor/manual/pushups
    name = "muscular skin"
    desc = "The reward for all your hard work. </br>THE INFLUENCE OF THE HAM SANDWYCH RACE IS WANING. I MUST DO PUSH-UPS, TO REMIND MY MUSCLES OF THEIR OWN STRENGTH."

    repairmsg_end = "My muscles sheen with vitality!"
    repairmsg_continue = "My muscles are reminded of their own strength."

/obj/item/clothing/suit/roguetown/armor/manual/pushups/get_mechanics_examine(mob/user)
	. = ..()

	. += span_info("Repairable via push-up emotes.")

/obj/item/clothing/suit/roguetown/armor/manual/pushups/leather
    armor = ARMOR_LEATHER

/obj/item/clothing/suit/roguetown/armor/manual/pushups/leather/good
    armor = ARMOR_LEATHER_GOOD


/*
 * SEWABLE ARMOUR
 */

/obj/item/clothing/suit/roguetown/armor/manual/sewable
	var/list/repair_items[] = list(
		/obj/item/needle = 'sound/foley/sewflesh.ogg'
	)

/obj/item/clothing/suit/roguetown/armor/manual/sewable/get_mechanics_examine(mob/user)
	. = ..()

	for(var/repair_thing in repair_items)
		var/obj/item/thing = new repair_thing
		. += span_info("Repairable via [thing.name].")

/obj/item/clothing/suit/roguetown/armor/manual/sewable/attackby(obj/item/I, mob/user, params)
	if(user != loc)
		return FALSE
	if(!repair_check(user, I))
		return FALSE

	if(!do_after(user, 5 SECONDS, target = src))
		return FALSE

	armour_regen()

	playsound(loc, repair_items[I.type], 100, TRUE)
	return TRUE

/obj/item/clothing/suit/roguetown/armor/manual/sewable/proc/repair_check(mob/user, obj/item/I)
	if(!(I.type in repair_items))
		return FALSE

	if(istype(I, /obj/item/needle))
		var/obj/item/needle/sew_needle = I
		if(!sew_needle.stringamt)
			to_chat(user, span_warning("[src] has no thread!"))
			return FALSE

	return TRUE


/obj/item/clothing/suit/roguetown/armor/manual/sewable/confessor
	name = "arbalist's skin"
	desc = "Taut lyke the bow I draw."
	armor = ARMOR_CLOTHING_GOOD
	max_integrity = ARMOR_INT_CHEST_CIVILIAN
	prevent_crits = list(BCLASS_CUT, BCLASS_BLUNT)
	repair_items = list(
		/obj/item/needle = 'sound/foley/sewflesh.ogg',
		/obj/item/rogueweapon/surgery/cautery = 'sound/surgery/cautery1.ogg',
	)

/obj/item/clothing/suit/roguetown/armor/manual/sewable/repair_check(mob/user, obj/item/I)
	. = ..()

	if(!.)
		return FALSE

	if(istype(I, /obj/item/rogueweapon/surgery/cautery))
		var/obj/item/rogueweapon/surgery/cautery/sew_cautery = I
		if(!sew_cautery.heated)
			to_chat(user, span_warning("[src] is not heated up!"))
			return FALSE

	return TRUE
